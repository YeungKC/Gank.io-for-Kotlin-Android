apply plugin: 'com.android.application'
//apply plugin: 'realm-android'
apply plugin: 'kotlin-android'
//apply plugin: 'kotlin-android-extensions'


def gitVersionCode() {
    def cmd = 'git rev-list HEAD --first-parent --count'
    cmd.execute().text.trim().toInteger()
}

def gitVersionTag() {
    def cmd = 'git describe --tags'
    def version = cmd.execute().text.trim()

    def pattern = "-(\\d+)-g"
    def matcher = version =~ pattern

    if (matcher) {
        version = version.substring(0, matcher.start()) + "." + matcher[0][1]
    } else {
        version = version + ".0"
    }

    return version
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultConfig {
        applicationId "gank.io.kc"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 2
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "API_BASE_URL", "\"${API_BASE_URL}\"")
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all { variant ->
        if (variant.buildType.name.equals('release')) {
            variant.mergedFlavor.versionCode = gitVersionCode()
            variant.mergedFlavor.versionName = gitVersionTag()
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    dataBinding.enabled = true
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })


    compile "com.android.support:support-v4:$support_version"
    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:design:$support_version"
    compile "com.android.support:cardview-v7:$support_version"
    compile "com.android.support:customtabs:$support_version"
    compile "com.android.support:customtabs:$support_version"
    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta4'

    compile "org.jetbrains.anko:anko-common:0.9"
    compile "org.jetbrains.anko:anko-sqlite:0.9"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile "com.squareup.retrofit2:adapter-rxjava:2.1.0"

    compile 'io.reactivex:rxandroid:1.0.1'

    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.facebook.stetho:stetho:1.3.1'
    compile "com.facebook.stetho:stetho-okhttp3:1.4.1"

    compile 'com.orhanobut:logger:1.15'

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile ('com.github.bumptech.glide:okhttp3-integration:+'){
        exclude group: 'glide-parent'
    }

    testCompile 'junit:junit:4.12'

    compile 'org.greenrobot:eventbus:3.0.0'

    kapt "com.android.databinding:compiler:2.2.2"

    compile 'ws.vinta:pangu:1.1.0'
}
kapt {
    generateStubs = true
}
repositories {
    mavenCentral()
}
